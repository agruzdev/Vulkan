cmake_minimum_required (VERSION 3.0)

macro(add_snippet Name Directory Default)
option(ENABLE_${Name} "Enable snippet ${Name}" ${Default})
if(ENABLE_${Name})
    set(sample_build_root "${CMAKE_BINARY_DIR}/${Name}")
    set(CONF_NAME ${Name})
    set(CONF_DIRECTORY ${Directory})
    configure_file("CMakeLists.txt.in" "${sample_build_root}/CMakeLists.txt" @ONLY)
    add_subdirectory(${sample_build_root} "${sample_build_root}/build")
    unset(CONF_DIRECTORY)
    unset(CONF_NAME)
    unset(sample_build_root)
endif()
unset(ENABLE_${Name})
endmacro()

project(VulkanSamples)

if(NOT MSVC OR MSVC_VERSION LESS 1900)
    message(FATAL_ERROR "Only MSVC 2015 target is supported!")
endif()

set(VULKAN_INCLUDE $ENV{VULKAN_SDK}/Include CACHE PATH "Vulkan include directory")
if("${CMAKE_SIZEOF_VOID_P}" EQUAL "8")
    set(VULKAN_LIBRARY $ENV{VULKAN_SDK}/Bin/vulkan-1.lib CACHE PATH "Vulkan library")
else()
    set(VULKAN_LIBRARY $ENV{VULKAN_SDK}/Bin32/vulkan-1.lib CACHE PATH "Vulkan library")
endif()

if(NOT EXISTS ${VULKAN_INCLUDE}/vulkan/vulkan.hpp)
    message(FATAL_ERROR "Vulkan inlcudes is not found!")
endif()
if(NOT EXISTS ${VULKAN_LIBRARY})
    message(FATAL_ERROR "Vulkan library is not found!")
endif()

set(SHADERS_DIR "${CMAKE_SOURCE_DIR}/shaders")
set(RESOURCES_DIR "${CMAKE_SOURCE_DIR}/res")

set(COMMON_DIR ${CMAKE_SOURCE_DIR}/samples/Common)
list(APPEND COMMON_INCLUDES ${COMMON_DIR})

add_subdirectory(${CMAKE_SOURCE_DIR}/samples/common)

add_snippet(01_Context          ${CMAKE_SOURCE_DIR}/samples/01_Context          ON)
add_snippet(02_CommandBuffers   ${CMAKE_SOURCE_DIR}/samples/02_CommandBuffers   ON)
add_snippet(03_Window           ${CMAKE_SOURCE_DIR}/samples/03_Window           ON)
add_snippet(04_DynamicCommands  ${CMAKE_SOURCE_DIR}/samples/04_DynamicCommands  ON)
add_snippet(05_SimpleTriangle   ${CMAKE_SOURCE_DIR}/samples/05_SimpleTriangle   ON)
add_snippet(06_AdvancedQuad     ${CMAKE_SOURCE_DIR}/samples/06_AdvancedQuad     ON)
add_snippet(07_SimpleShading    ${CMAKE_SOURCE_DIR}/samples/07_SimpleShading    ON)
add_snippet(08_InteractiveCube  ${CMAKE_SOURCE_DIR}/samples/08_InteractiveCube  ON)
add_snippet(09_Texture          ${CMAKE_SOURCE_DIR}/samples/09_Texture          ON)
add_snippet(12_Animation        ${CMAKE_SOURCE_DIR}/samples/12_Animation        ON)
add_snippet(13_GeometryShader   ${CMAKE_SOURCE_DIR}/samples/13_GeometryShader   ON)
add_snippet(14_FurRendering     ${CMAKE_SOURCE_DIR}/samples/14_FurRendering     ON)

add_snippet(10_ComputePipeline  ${CMAKE_SOURCE_DIR}/samples/10_ComputePipeline  ON)
add_snippet(11_HeatComputation  ${CMAKE_SOURCE_DIR}/samples/11_HeatComputation  ON)
